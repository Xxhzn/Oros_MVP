extends Resource
class_name SkillData

## --- 核心要素枚举 ---

enum SkillType { 
	ACTIVE,  # 主动技能，由玩家/AI选择施放
	PASSIVE  # 被动技能，在满足条件时自动生效
}

## 目标类型
enum TargetType {
	NONE,                   ## 无需目标 (例如自身buff)
	ENEMY_SINGLE,           ## 敌方单体
	ENEMY_ALL,              ## 敌方全体
	ALLY_SINGLE,            ## 我方单体 (不含自己)
	ALLY_ALL,               ## 我方全体 (不含自己)
	SELF,                   ## 施法者自己
	ALLY_SINGLE_INC_SELF,   ## 我方单体 (含自己)
	ALLY_ALL_INC_SELF,      ## 我方全体 (含自己)
	ENEMY_RANDOM,           ## 敌方随机
	ALLY_RANDOM,            ## 我方随机 (不含自己)
	ALLY_RANDOM_INC_SELF    ## 我方随机 (含自己)
}

#region --- 导出的属性 ---
@export var skill_id: StringName = &"new_skill" 				## 内部ID，用StringName效率略高
@export var skill_name: String = "新技能"       					## UI显示名称
@export var skill_type: SkillType = SkillType.ACTIVE			## 技能类型
@export_multiline var description: String = "技能描述..." 		## UI显示描述
@export var is_melee: bool = true								## 是否是近战

@export_group("消耗与目标")
@export var skill_cd: int = 5									## 冷却回合
@export var skill_cd_last: int									## 技能剩余冷却回合
@export var target_type: TargetType = TargetType.ENEMY_SINGLE	## 目标类型
@export_range(0, 10) var target_count: int = 1 					## 目标数量，仅对多目标类型有效
@export var can_target_dead : bool = false 						## 是否可以对死亡目标施放

@export_group("效果")

@export var attack_count : int									## 技能攻击次数
@export var attack_rate : float									## 技能伤害倍率
										
@export_group("视觉与音效 (可选)")
@export var icon: Texture2D = null 								## 技能图标
@export var cast_animation: StringName = "" 					## 施法动画名 (如果角色动画器中有)
@export var pre_cast_delay: float = 0.2							## 释放前延迟
@export var post_cast_delay: float = 0.0						## 释放后延迟

# 未来可扩展其他视觉和音效选项
# @export var vfx_scene: PackedScene # 技能特效场景
# @export var sfx: AudioStream # 技能音效
#endregion

## 检查是否能施放技能
func can_cast(current_cd_last: float) -> bool:
	return current_cd_last == 0

## 获得技能完整描述
func get_full_description() -> String:
	var desc = ""
	desc += "类型: 主动技能\n"
	desc += "冷却时间: " + str(skill_cd) + " 回合\n"
	desc += "目标: " + _get_target_type_name() + "\n"

	#desc += "\n效果:\n"
	#var effects_to_describe: Array[Keywords.keywords] = effects
	#for effect in effects_to_describe: # 处理 ACTIVE 和 PASSIVE 的主要效果
		#if effect.disable:
			#continue
		#if is_instance_valid(effect): # 确保 effect 实例有效
			#desc += "- " + effect.get_description() + "\n"
		#else:
			#desc += "- [color=red](无效效果数据)[/color]\n"
	desc += "[color=gray]" + description + "[/color]\n\n"
	return desc.strip_edges()

## 是否需要选择目标
func needs_target() -> bool:
	return target_type in [TargetType.ENEMY_SINGLE, TargetType.ALLY_SINGLE, TargetType.ALLY_SINGLE_INC_SELF]

## 敌人目标
func is_enemy_target() -> bool:
	return target_type in [TargetType.ENEMY_SINGLE, TargetType.ENEMY_ALL]

## 包含自身
func is_including_self() -> bool:
	return target_type in [TargetType.ALLY_SINGLE_INC_SELF, TargetType.ALLY_ALL_INC_SELF]

## 获取目标类型名称
func _get_target_type_name() -> String:
	match target_type:
		TargetType.SELF:
			return "自身"
		TargetType.ENEMY_SINGLE:
			return "单个敌人"
		TargetType.ENEMY_ALL:
			return "所有敌人"
		TargetType.ALLY_SINGLE:
			return "单个友方(不含自己)"
		TargetType.ALLY_ALL:
			return "所有友方(不含自己)"
		TargetType.ALLY_SINGLE_INC_SELF:
			return "单个友方(含自己)"
		TargetType.ALLY_ALL_INC_SELF:
			return "所有友方(含自己)"
		_:
			return "未知目标"
